import pathlib

Import('env')

single_env = env.Clone(TEMPLATEDIR='templates')

single_env.LyGeometry('geometry')
single_env.TexGeometry('geometry')

for source in single_env.Glob('$BODYDIR/*.yaml'):
    basename = pathlib.Path(str(source)).stem
    music_wrapper = single_env.LilypondWrapper(f'{basename}_hymnal', [source])
    music_file = single_env.Lilypond(f'{basename}_hymnal', music_wrapper)
    texfile = single_env.Wrapper(basename, ['$TEMPLATEDIR/single.tex.tmp', source] + music_file)
    if GetOption('twoup'):
        oneup_pdf = single_env.PDF(f'{basename}_1up.pdf', texfile)
        pdf_result = single_env.TwoUp(f'{basename}.pdf', oneup_pdf)
    else:
        pdf_result = single_env.PDF(basename, texfile)
